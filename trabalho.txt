0 * * l 0
0 _ | r 34

;##############################################################################
; PRIMEIRA PARTE
;
; Realiza o complemento de 2
;##############################################################################

; Caminha para a direita invertendo números até encontrar espaço
1 0 1 r 1
1 1 0 r 1
1 = * l 2

; Somando 1
2 0 1 l 3
2 1 0 l 4

; Caminha para a esquerda repetindo o que lê
3 0 * l 3
3 1 * l 3
3 - * * 6 ; Realiza a soma

; Caminha para a esquerda invertendo números até encontrar sinal de igualdade
4 1 0 l 4
4 0 1 l 3
4 - * * 6 ; Realiza a soma

;##############################################################################
; SEGUNDA PARTE
;
; Realiza a cópia de elementos da parte esquerda de um operador
; para a parte esquerda do delimitador '|', para adição
; 
; a = 0 já lido
; b = 1 já lido
; | = delimitador
;##############################################################################

5 0 * l 5
5 1 * l 5
5 _ * r 6

6 0 * r 6
6 1 * r 6
6 | * r 6
6 * * * 7

7 0 a l 8
7 1 b l 9
7 | * r 10
7 * * l 7

8 * * l 8
8 _ 0 r 6

9 * * l 9
9 _ 1 r 6

;##############################################################################
; TERCEIRA PARTE
;
; Procura operador
;##############################################################################

10 b 1 r 10
10 a 0 r 10
10 + * * 11
10 - * r 11 ; É necessário realizar o complemento de 2 

;##############################################################################
; QUARTA PARTE
;
; Realiza a soma binária
;##############################################################################

11 * * r 11
11 = * l 12

12 * * l 12
12 0 a * 14
12 1 b * 13
12 + * * 19 ; Estado que move os números para frente da igualdade
12 - * * 19 ; Estado que move os números para frente da igualdade

13 * * l 13
13 | * l 15 ; Estado para soma 1

14 * * l 14
14 | * l 18 ; Estado soma 0

15 * * l 15
15 1 0 l 15	; Soma 1 e vai 1
15 _ 1 * 16 
15 0 1 * 16 ; Soma 1

16 * * r 16
16 | * l 17

17 0 a * 11
17 1 b * 11
17 * * l 17

18 * * l 18
18 _ 0 * 16
18 0 a r 11 ; 0 + 0
18 1 b r 11 ; 0 + 1

;##############################################################################
; QUINTA PARTE
;
; Move resultado para a parte direita da igualdade
;##############################################################################

19 * * l 19
19 _ _ r 20

20 b _ r 21
20 1 _ r 21
20 a _ r 22
20 0 _ r 22
20 | _ r 25

21 * * r 21
21 = * r 23

22 * * r 22
22 = * r 24

23 * * r 23
23 _ 1 * 19

24 * * r 24
24 _ 0 * 19

;##############################################################################
; SEXTA PARTE
;
; Formata o resultado
;##############################################################################

25 * * r 25
25 a 0 r 25
25 b 1 r 25
25 = * l 29

;##############################################################################
; SÉTIMA PARTE
;
; Checa se é necessário complemento de 2
;##############################################################################

27 * * l 27
27 - * r 28 ; encontrou um sinal de -, é necessário complemento de 2
27 + * * 29

28 * * r 28
28 = * r 1

29 * * * halt-accept

;##############################################################################
; OITAVA PARTE
;
; Realiza a cópia de elementos da parte direita de um operador
; para a parte esquerda do delimitador '|', para subtração
; 
; a = 0 já lido
; b = 1 já lido
; | = delimitador
;##############################################################################

30 * * r 30
30 = * l 31

31 * * l 31
31 1 b * 32
31 0 a * 33
31 - * * 11

32 * * l 32
32 _ 1 * 30

33 * * l 33
33 _ 0 * 30

;##############################################################################
; NONA PARTE
;
; Checa tipo de operação a ser feita
;##############################################################################

34 * * r 34
34 + * l 35
34 - * r 1

;##############################################################################
; DÉCIMA PARTE
;
; Reposiciona cabeçote para soma
;##############################################################################

35 * * l 35
35 _ * * 5 ; Cópia de elementos para adição

;##############################################################################
; DÉCIMA PRIMEIRA PARTE
;
; Reposiciona cabeçote para subtração
;##############################################################################

36 * * l 36
36 _ * * 1 ; Cópia de elementos para subtração