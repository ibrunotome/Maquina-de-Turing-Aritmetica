0 * * * 5

;##############################################################################
; PRIMEIRA PARTE
;
; Função que realiza o complemento de 2
;##############################################################################

; Complemento de 2, inverte números até encontrar espaço
1 0 1 r 1
1 1 0 r 1
1 _ _ l 2

; Complemento de 2, somando 1
2 0 1 l 3
2 1 0 l 4

; Caminha para a esquerda repetindo o que lê
3 0 0 l 3
3 1 1 l 3
3 _ * * 5

; Caminha para a esquerda invertendo o número
4 1 0 l 4
4 0 1 l 3
4 _ * * 5

;##############################################################################
; SEGUNDA PARTE
;
; Função que realiza a cópia de elementos da parte esquerda de um operador
; para a parte esquerda do delimitador '|'
; a = 0 já lido
; b = 1 já lido
; | = delimitador
;##############################################################################

; Copia elementos da parte esquerda da operação
5 0 * l 5
5 1 * l 5
5 _ | r 6

6 0 * r 6
6 1 * r 6
6 | * r 6
6 * * * 7

7 0 a l 8
7 1 b l 9
7 | * r 10
7 * * l 7

8 * * l 8
8 _ 0 r 6

9 * * l 9
9 _ 1 r 6

;##############################################################################
; TERCEIRA PARTE
;
; Procura operador
;##############################################################################

10 b 1 r 10
10 a 0 r 10
10 + * * 11

;##############################################################################
; QUARTA PARTE
;
; Realiza a soma binária
;##############################################################################

11 * * r 11
11 = * l 12

12 * * l 12
12 0 a * 14
12 1 b * 13
12 + * * 19 ; Estado que move os números para frente da igualdade

13 * * l 13
13 | * l 15 ; Estado para soma 1

14 * * l 14
14 | * l 18 ; Estado soma 0

15 * * l 15
15 1 0 l 15	; Soma 1 e vai 1
15 _ 1 * 16 
15 0 1 * 16 ; Soma 1

16 * * r 16
16 | * l 17

17 0 a * 11
17 1 b * 11
17 * * l 17

18 * * l 18
18 _ 0 * 16
18 0 a r 11 ; 0 + 0
18 1 b r 11 ; 0 + 1

;##############################################################################
; QUINTA PARTE
;
; Move resultado para a parte direita da igualdade
;##############################################################################

19 * * l 19
19 _ _ r 20

20 b _ r 21
20 1 _ r 21
20 a _ r 22
20 0 _ r 22
20 | _ r 25 ; halt-accept 

21 * * r 21
21 = * r 23

22 * * r 22
22 = * r 24

23 * * r 23
23 _ 1 * 19

24 * * r 24
24 _ 0 * 19

;##############################################################################
; SEXTA PARTE
;
; Formata o resultado
;##############################################################################

25 * * r 25
25 a 0 r 25
25 b 1 r 25
25 _ * * halt-accept